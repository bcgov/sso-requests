module "s3_keycloak_v2" {
  source      = "./s3-bucket"
  bucket_name = "xgr00q-dev-keycloak"
}

module "s3_sso_api_swagger" {
  source           = "./s3-bucket"
  bucket_name      = "xgr00q-dev-api-swagger"
  enable_encrytion = false
}

resource "aws_dynamodb_table" "state_locking" {
  hash_key = "LockID"
  name     = "xgr00q-dev-state-locking"
  attribute {
    name = "LockID"
    type = "S"
  }
  billing_mode = "PAY_PER_REQUEST"
}

resource "aws_lambda_function" "siteminder-tests-scheduler" {
  filename      = "lambda-siteminder-tests-scheduler.zip"
  function_name = "lambda-siteminder-tests-scheduler"
  role          = aws_iam_role.iam_for_lambda.arn
  # has to have the form filename.functionname where filename is the file containing the export
  handler = "index.handler"

  # The filebase64sha256() function is available in Terraform 0.11.12 and later
  # For Terraform 0.11.11 and earlier, use the base64sha256() function and the file() function:
  # source_code_hash = "${base64sha256(file("lambda_function_payload.zip"))}"
  source_code_hash = filebase64sha256("lambda-siteminder-tests-scheduler.zip")

  runtime = "nodejs12.x"

  vpc_config {
    subnet_ids         = [data.aws_subnet.a.id, data.aws_subnet.b.id]
    security_group_ids = [aws_security_group.rds_sg.id]
  }

  environment {
    variables = {
      NODE_ENV        = "production"
      GH_OWNER        = var.gh_owner
      GH_ACCESS_TOKEN = var.gh_access_token
    }
  }

  timeout = 45

  tags = {
    "managed-by" = "terraform"
  }
}

resource "aws_cloudwatch_event_rule" "siteminder-tests-scheduler" {
  name                = "siteminder-tests-scheduler"
  schedule_expression = "cron(0 12 * * ? *)"
  # run every day at 05:00 AM PST (12:00 pm UTC)
}

resource "aws_cloudwatch_event_target" "siteminder-tests-scheduler" {
  rule      = aws_cloudwatch_event_rule.siteminder-tests-scheduler.name
  target_id = "siteminder-tests-scheduler"
  arn       = aws_lambda_function.siteminder-tests-scheduler.arn
}

resource "aws_lambda_permission" "siteminder-tests-scheduler" {
  statement_id  = "AllowExecutionFromCloudWatch"
  action        = "lambda:InvokeFunction"
  function_name = aws_lambda_function.siteminder-tests-scheduler.function_name
  principal     = "events.amazonaws.com"
  source_arn    = aws_cloudwatch_event_rule.siteminder-tests-scheduler.arn
}
